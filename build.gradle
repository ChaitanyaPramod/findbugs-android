plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.1'
    id 'com.gradle.plugin-publish' version '0.9.4'
}

gradlePlugin {
    plugins {
        findbugsAndroidPlugin {
            id = 'com.chaitanyapramod.findbugs-android'
            implementationClass = 'com.chaitanyapramod.gradle.android.findbugs.FindBugsAndroidPlugin'
        }
    }
}

group 'com.chaitanyapramod.gradle'
version '1.1'

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def pomConfig = {
    licenses {
        license {
            name 'MIT License'
            url 'https://opensource.org/licenses/mit-license.php'
        }
    }
    developers {
        developer {
            id 'ChaitanyaPramod'
            name 'Chaitanya Pramod'
            email 'me@chaitanyapramod.com'
        }
    }
    scm {
        url 'https://github.com/ChaitanyaPramod/findbugs-android.git'
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java

            artifact sourceJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin that creates FindBugs reports for android projects')
                root.appendNode('name', 'findbugs-android-gradle-plugin')
                root.appendNode('url', 'https://findbugs-android.chaitanyapramod.com/')
                root.children().last() + pomConfig
           }
        }
    }
}

bintray {
    user = property('bintray.user')
    key = property('bintray.key')
    pkg {
        publications = ['plugin']
        repo = 'maven'
        name = 'findbugs-android-gradle-plugin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/ChaitanyaPramod/findbugs-android.git'
        websiteUrl = 'https://github.com/ChaitanyaPramod/findbugs-android'
        issueTrackerUrl = 'https://github.com/ChaitanyaPramod/findbugs-android/issues'
        githubRepo = 'ChaitanyaPramod/findbugs-android'
        githubReleaseNotesFile = 'CHANGELOG.md'

        labels = ['gradle', 'findbugs', 'plugin', 'android']
        version {
            gpg {
                sign = true
            }
        }
    }
}
pluginBundle {
    website = 'https://github.com/ChaitanyaPramod/findbugs-android'
    vcsUrl = 'https://github.com/ChaitanyaPramod/findbugs-android.git'
    description = 'Gradle plugin that creates FindBugs tasks for each variant of android application or library project'
    tags = ['android', 'findbugs']

    plugins {
        findbugsAndroidPlugin {
            id = 'com.chaitanyapramod.findbugs-android'
            displayName = 'Findbugs Android plugin'
        }
    }

    mavenCoordinates {
        groupId = 'com.chaitanyapramod.gradle'
        artifactId = 'findbugs-android'
        version = project.version
    }
}
